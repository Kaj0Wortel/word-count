plugins {
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
    id 'java'
    id 'war'
}

import io.franzbecker.gradle.lombok.task.DelombokTask

group 'net.projecteuler'
version '1.0'

compileJava {
    targetCompatibility = '11'
    sourceCompatibility = '11'
}

repositories {
    mavenCentral()
    // For including a local jar file:
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    providedCompile 'jakarta.platform:jakarta.jakartaee-api:9.1.0'
}

test {
    useJUnitPlatform()
}

configurations {
    compile
}
task delombok(type: DelombokTask, dependsOn: compileJava) {
    ext.outputDir = file("$buildDir/delombok")
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each {
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

task zipSrc(type: Zip) {
    classifier = 'src'
    from sourceSets*.allSource
}

task export(type: Copy) {
    dependsOn clean
    dependsOn war
    dependsOn zipSrc
    dependsOn javadoc

    from 'build/libs', 'build/docs', 'build/distributions'
    into 'build/export'
}
